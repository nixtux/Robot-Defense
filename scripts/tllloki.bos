piece base,body,turret,sleeve,barrel,flare;

static-var restore_delay,fire;

#define SIG_AIM 2
#define SMOKEPIECE1 turret
#include "smokeunit.h"
#include "rockunit.h"

Create()
{ hide flare;
  restore_delay=3000;
  start-script SmokeUnit();
 }

SetMaxReloadTime(time)
{ restore_delay = time * 2;
 }

RestoreAfterDelay()
{ set-signal-mask SIG_AIM;
  sleep restore_delay;
  turn turret to y-axis <0> speed <50>;
  turn sleeve to x-axis <0> speed <50>;
  wait-for-turn turret around y-axis;
  wait-for-turn sleeve around x-axis;
 }

AimPrimary(heading,pitch)
{ signal SIG_AIM;
  set-signal-mask SIG_AIM;
  turn turret to y-axis heading speed <50>;
  turn sleeve to x-axis (0-pitch) speed <50>;
  wait-for-turn turret around y-axis;
  wait-for-turn sleeve around x-axis;
  start-script RestoreAfterDelay();
  return(TRUE);
 }

FirePrimary()
{ show flare;
  move barrel to z-axis [-2.5] speed [500];
  sleep 250;
  hide flare;
  move barrel to z-axis [0] speed [5];
 }

AimFromPrimary(piecenum)
{ piecenum=flare;
 }

QueryPrimary(piecenum)
{ piecenum=flare;
 }

SweetSpot(piecenum)
{ piecenum=turret;
 }

Killed( severity, corpsetype )
{ 
  explode base type BITMAPONLY | BITMAP4;
  if (severity <= 25)
   { corpsetype = 1;
     explode sleeve type BITMAPONLY | BITMAP4;
     explode turret type BITMAPONLY | BITMAP5;
     return( 0 );
    }
  explode sleeve type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
  explode barrel type FALL | SMOKE | EXPLODE_ON_HIT | BITMAP2;
  if (severity <= 50)
   { corpsetype = 2;
     return( 0 );
    }
  corpsetype = 3;
  explode turret type SHATTER | EXPLODE_ON_HIT | BITMAP2;
 }
