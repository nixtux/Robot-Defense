// by Nikola Genkov, nikola_g@yahoo.com

piece base,body,turret,axis,barrel,flare;

static-var restore_delay;

#define SIG_AIM 2
#define SMOKEPIECE1 turret
#include "smokeunit.h"
#include "exptype.h"
#include "hitweap.h"

Create()
{ hide flare;
  restore_delay=3000;
  start-script SmokeUnit();
 }

SetMaxReloadTime(time)
{ restore_delay = time * 2;
 }

RestoreAfterDelay()
{ set-signal-mask SIG_AIM;
  sleep restore_delay;
  turn turret to y-axis 0 speed <90>;
  turn axis to x-axis 0 speed <60>;
 }

AimPrimary(heading,pitch)
{ signal SIG_AIM;
  set-signal-mask SIG_AIM;
  turn turret to y-axis heading speed <90>;
  turn axis to x-axis (0-pitch) speed <60>;
  wait-for-turn turret around y-axis;
  wait-for-turn axis around x-axis;
  start-script RestoreAfterDelay();
  return(TRUE);
 }

FirePrimary()
{ show flare;
  move barrel to z-axis [-1] speed [500];
  sleep 150;
  hide flare;
  move barrel to z-axis [0] speed [1];
 }

AimFromPrimary(piecenum)
{ piecenum=turret;
 }

QueryPrimary(piecenum)
{ piecenum=flare;
 }

SweetSpot(piecenum)
{ piecenum=body;
 }

Killed( severity, corpsetype )
{ hide flare;
  explode base type BITMAPONLY | BITMAP4;
  if (severity <= 25)
   { corpsetype = 1;
     explode barrel type BITMAPONLY | BITMAP2;
     explode turret type BITMAPONLY | BITMAP3;
     return( 0 );
    }
  explode turret type FALL | SMOKE | EXPLODE_ON_HIT | BITMAP2;
  explode barrel type FALL | SMOKE | EXPLODE_ON_HIT | BITMAP2;
  if (severity <= 50)
   { corpsetype = 2;
     return( 0 );
    }
  corpsetype = 3;
  explode body type SHATTER | EXPLODE_ON_HIT | BITMAP3;
  return( 0 );
 }
