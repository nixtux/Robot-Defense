// by Nikola Genkov, nikola_g@yahoo.com

piece base,tower,turret,axis,holder,bearing,barrel1,barrel2,flare1,flare2;

static-var barrel;

#define SIG_AIM	2
#define SMOKEPIECE1 holder

#include "EXPtype.h"
#include "smokeunit.h"

Create()
 { hide flare1;
   hide flare2;
   dont-shade tower;
   dont-shade turret;
   dont-shade flare1;
   dont-shade flare2;
   dont-shade barrel1;
   dont-shade barrel2;
   dont-shade holder;
   cache tower;
   cache turret;
   cache flare1;
   cache flare2;
   cache barrel1;
   cache barrel2;
   cache holder;
   barrel=0;   
   turn bearing to x-axis <50> NOW;
   start-script SmokeUnit();
  }

AimPrimary(heading,pitch)
 { signal SIG_AIM;
   set-signal-mask SIG_AIM;
   turn turret to y-axis heading speed <100>;
   turn axis to x-axis (0-pitch) speed <60>;
   wait-for-turn turret around y-axis;
   wait-for-turn axis around x-axis;
   return(TRUE);
  }

FirePrimary()
 { if (barrel==0)
    { show flare1; 
      move barrel1 to z-axis [-4] speed [500];
      sleep 200; hide flare1; 
      move barrel1 to z-axis [0] speed [5];
      turn holder to x-axis <-50> speed <20>;
      barrel=1; return 0; }
   if (barrel==1)
    { show flare2;
      move barrel2 to z-axis [-4] speed [500];
      sleep 200; hide flare2; 
      move barrel2 to z-axis [0] speed [5];
      turn holder to x-axis <0> speed <20>;
      barrel=0; return 0; }
  }

AimFromPrimary(piecenum)
 { piecenum=holder;
  }

QueryPrimary(piecenum)
 { piecenum=flare1;
   if (barrel==1) { piecenum=flare2; }
  }

SweetSpot(piecenum)
 { piecenum=turret;
  }

Killed( severity, corpsetype )
 { hide flare1;
   hide flare2;
   explode barrel1 type FALL | SMOKE | EXPLODE_ON_HIT | BITMAP1;
   explode barrel2 type FALL | SMOKE | EXPLODE_ON_HIT | BITMAP1;
   explode holder type FALL | SMOKE | EXPLODE_ON_HIT | BITMAP1;
   explode turret type SHATTER | EXPLODE_ON_HIT | BITMAP2;
   explode turret type BITMAPONLY | BITMAP4;
   if (severity <= 25)
    { corpsetype = 1;
      return( 0 );
     }
   if (severity <= 50)
    { corpsetype = 2;
      return( 0 );
     }
   corpsetype = 3;
  }
